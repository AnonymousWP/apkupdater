plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.apkupdater'
    compileSdk 34

    defaultConfig {
        applicationId "com.apkupdater"
        minSdk 24
        targetSdk 34
        versionCode 46
        versionName "3.0.0-beta-03"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables { useSupportLibrary true }
    }

    signingConfigs {
        release {
            try {
                Properties props = new Properties()
                props.load(new FileInputStream(file("../local.properties")))
                storeFile file(props['keystore.file'])
                storePassword props['keystore.password']
                keyAlias props['keystore.keyalias']
                keyPassword props['keystore.keypassword']
            } catch (ignored) {
                storeFile signingConfigs.debug.storeFile
                storePassword signingConfigs.debug.storePassword
                keyAlias signingConfigs.debug.keyAlias
                keyPassword signingConfigs.debug.keyPassword
                println "Signing config not found. Using debug settings."
            }
            v1SigningEnabled true
            v2SigningEnabled true
            enableV3Signing true
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.configureEach {
        outputs.configureEach {
            outputFileName = defaultConfig.applicationId + "." + buildType.name + "." + defaultConfig.versionCode + ".apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions { jvmTarget = '1.8' }
    buildFeatures { compose true }
    composeOptions { kotlinCompilerExtensionVersion '1.5.1' }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    lint {
        disable 'ExtraTranslation', 'MissingTranslation'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.1'
    implementation 'androidx.compose.ui:ui:1.5.0'
    implementation 'androidx.tv:tv-foundation:1.0.0-alpha08'
    implementation 'androidx.compose.material3:material3:1.2.0-alpha05'
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    implementation 'io.insert-koin:koin-android:3.4.2'
    implementation 'io.insert-koin:koin-androidx-compose:3.4.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.coil-kt:coil-compose:2.4.0'
    implementation 'com.github.rumboalla.KryptoPrefs:kryptoprefs:0.4.3'
    implementation 'com.github.rumboalla.KryptoPrefs:kryptoprefs-gson:0.4.3'
    implementation 'org.jsoup:jsoup:1.16.1'
    implementation 'eu.chainfire:libsuperuser:1.1.1'
    implementation 'io.github.g00fy2:versioncompare:1.5.0'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.5.0'

    debugImplementation 'androidx.compose.ui:ui-tooling:1.5.0'
    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.5.0'

}
